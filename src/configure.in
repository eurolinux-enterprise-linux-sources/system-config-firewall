# Process this file with autoconf to produce a configure script.

AC_PREREQ([2.63])

m4_define([PKG_NAME], system-config-firewall)
m4_define([PKG_VERSION], m4_bpatsubst(m4_esyscmd([grep "Version:" system-config-firewall.spec]), [Version:\W\([0-9.]*\)\W], [\1]))
m4_define([PKG_RELEASE], m4_bpatsubst(m4_esyscmd([grep "Release:" system-config-firewall.spec]), [Release:\W\([0-9.]*\).*\W], [\1]))
m4_define([PKG_TAG], m4_bpatsubst(m4_format(r%s-%s, PKG_VERSION, PKG_RELEASE), [\.], [_]))

AC_INIT(PKG_NAME, PKG_VERSION)
AC_CONFIG_SRCDIR([src/lokkit])
AM_INIT_AUTOMAKE([1.9 tar-ustar no-define foreign dist-bzip2 no-dist-gzip])

AC_SUBST([PACKAGE_RELEASE], '[PKG_RELEASE]')
AC_DEFINE_UNQUOTED([PACKAGE_RELEASE], ["$PACKAGE_RELEASE"])
AC_SUBST([PACKAGE_TAG], '[PKG_TAG]')
AC_DEFINE_UNQUOTED([PACKAGE_TAG], ["$PACKAGE_TAG"])

AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET
AM_PATH_PYTHON

#############################################################

AC_ARG_ENABLE([usermode], AS_HELP_STRING([--enable-usermode], [Enable usermode support]), [use_usermode=$enableval], [use_usermode=no])
#test "$use_usermode" = "yes" && AC_DEFINE(USE_USERMODE)
AM_CONDITIONAL(USE_USERMODE, test x$use_usermode = xyes)
#AC_SUBST(USE_USERMODE)

AC_ARG_ENABLE([policykit0], AS_HELP_STRING([--enable-policykit0], [Enable policykit version 0 support]), [use_policykit0=$enableval], [use_policykit0=no])
#test "$use_policykit0" = "yes" && AC_DEFINE(USE_POLICYKIT0)
AM_CONDITIONAL(USE_POLICYKIT0, test x$use_policykit0 = xyes)
#AC_SUBST(USE_POLICYKIT0)

AC_ARG_ENABLE([policykit1], AS_HELP_STRING([--enable-policykit1], [Enable policykit version 1 support]), [use_policykit1=$enableval], [use_policykit1=yes])
#test "$use_policykit1" = "yes" && AC_DEFINE(USE_POLICYKIT1)
AM_CONDITIONAL(USE_POLICYKIT1, test x$use_policykit1 = xyes)
#AC_SUBST(USE_POLICYKIT1)

AM_CONDITIONAL(USE_DBUS, test x$use_policykit0 = xyes -o x$use_policykit1 = xyes)

#############################################################

AC_SUBST([GETTEXT_PACKAGE], '[PKG_NAME]')
AC_DEFINE_UNQUOTED([GETTEXT_PACKAGE], ["$GETTEXT_PACKAGE"],)

IT_PROG_INTLTOOL([0.35.0], [no-xml])
AM_PO_SUBDIRS

AC_CONFIG_FILES([Makefile
                 config/Makefile
                 po/Makefile.in
		 src/fw_config.py
		 src/Makefile])
AC_OUTPUT
